; This is the authoritative source for the USV specification.

file = *(annotation / group)
annotation = TEXTDATA

group = GS [ groupannotation ] groupdata [ groupterminators ]
groupannotation = TEXTDATA
groupdata =  *CRLF 1*(record)
groupterminators = ETB

record = RS *CRLF 1*(unit)
unit = US TEXTDATA

eGS = DLE GS ; Escaped Group Separator
eRS = DLE RS ; Escaped Record Separator
eUS = DLE US ; Escaped Unit Separator

TEXTDATA =  *(%x0A-0B / %x20-7E / eGS / eRS / eUS)

SOH = %x01 ; Start of Header
DLE = %x10 ; Data Link Escape
ETB = %x17 ; End of Transmission
GS  = %x1D ; Group Separator
RS  = %x1E ; Record separator
US  = %x1F ; Unit separator

CR = %x0D ;as per section 6.1 of RFC 2234
LF = %x0A ;as per section 6.1 of RFC 2234
CRLF = CR / LF / (CR LF) ;as per section 6.1 of RFC 2234
